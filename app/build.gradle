plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}

android {
    compileSdk rootProject.ext.android.compileSdk
    
    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdk rootProject.ext.android.minSdk
        targetSdk rootProject.ext.android.targetSdk
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        ndkVersion rootProject.ext.android.ndkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        ndk {
            abiFilters "arm64-v8a"
//            abiFilters "armeabi-v7a", "x86", "arm64-v8a", "x86_64"
        }

        buildConfigField "String", "VERSION_DATE", "\"$dayStr\""
        buildConfigField "int", "ENV_TYPE", "0"
        buildConfigField("String", "SOFT_CODE", "\"${rootProject.ext.softCode.topInfrared}\"")
        buildConfigField("String", "APP_KEY", "\"${rootProject.ext.appKey.topInfrared}\"")
        buildConfigField("String", "APP_SECRET", "\"${rootProject.ext.appSecret.topInfrared}\"")
        manifestPlaceholders = [
                JPUSH_PKGNAME : applicationId,
                JPUSH_APPKEY : "cbd4eafc9049d751fc5a8c58",
                JPUSH_CHANNEL : "developer-default",
                app_name: "IRCamera",
        ]

        setProperty("archivesBaseName", "TC001-v${versionName}.google")
    }

    bundle {
        language {
            enableSplit = false
        }
    }

    signingConfigs {
        release {
            storeFile file('artibox_key/ArtiBox.jks')
            keyAlias 'Artibox'
            storePassword 'artibox2017'
            keyPassword 'artibox2017'
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }


    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        versionApk(variant, defaultConfig)
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        //解决第三方so依赖冲突 merge（合并）  pickFirst（选取第一个）
        //如果合并不能解决问题就选择其中一个
        merge 'META-INF/LICENSE-notice.md'
        merge 'META-INF/LICENSE.md'
        merge 'META-INF/proguard/androidx-annotations.pro'
        merge 'META-INF/proguard/coroutines.pro'
//        merge 'lib/arm64-v8a/libc++_shared.so'
//        merge 'lib/armeabi-v7a/libc++_shared.so'
        pickFirst 'lib/x86/libc++_shared.so'
        pickFirst 'lib/x86_64/libc++_shared.so'
        pickFirst 'lib/arm64-v8a/libc++_shared.so'
        pickFirst 'lib/armeabi-v7a/libc++_shared.so'
        pickFirst 'META-INF/LICENSE.md'
        pickFirst 'META-INF/LICENSE-notice.md'
        pickFirst 'lib/arm64-v8a/libnative-window.so'
        pickFirst 'lib/armeabi-v7a/libnative-window.so'
        pickFirst 'lib/armeabi-v7a/libyuv.so'
        pickFirst 'lib/arm64-v8a/libyuv.so'
        pickFirst 'lib/armeabi-v7a/libopencv_java4.so'
        pickFirst 'lib/arm64-v8a/libopencv_java4.so'
        pickFirst 'lib/armeabi-v7a/libomp.so'
        pickFirst 'lib/arm64-v8a/libomp.so'
        pickFirst 'lib/arm64-v8a/liblog.so'
        pickFirst 'lib/armeabi-v7a/liblog.so'
        doNotStrip "**/*.so"
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures{
        dataBinding = true
    }
}
static def getYearStr() {
    return new Date().format('yy')
}
static def getDayStr() {
    return new Date().format('yyMMdd')
}

static def getTimeStr() {
    return new Date().format('HHmm')
}

def versionApk(variant, defaultConfig) {
    variant.outputs.all {
        def nameStr = "TopInfrared_" + defaultConfig.versionName + "." + dayStr
        def appName = nameStr + ".apk"
        outputFileName = appName
    }
}

//复制apk
def copyApk(type) {
    copy {
        from "build/outputs/apk/${type}"
        into "../outputs/${type}/"
        include '**/*.apk'
        rename { String fileName ->
            fileName.substring(0, fileName.lastIndexOf(".apk")) + '_build' + timeStr + '.apk'
        }
    }
}

//复制aab
def copyAab(type) {
    copy {
        from "build/outputs/bundle/${type}"
        into "../outputs/bundle/${type}/"
        include '**/*.aab'
        rename { String fileName ->
            fileName.substring(0, fileName.lastIndexOf(".google")) + "." + dayStr + "_" + type + '_build' + timeStr + '.aab'
        }
    }
}

project.afterEvaluate {
    assembleRelease.doLast {
        println "===================== build Release.apk copy ======================="
        copyApk("release")
    }
    bundleRelease.doLast {
        println "===================== build Release.aab copy ======================="
        copyAab("release")
    }
}

dependencies {

    implementation project(':component:pseudo')
    implementation project(':component:thermal-ir')
    implementation project(':component:thermal-lite')
    implementation project(':component:transfer')
    implementation project(':component:user')
    implementation project(':libapp')
    implementation project(':libcom')
    implementation project(':libir')
    implementation project(':libmenu')
    implementation project(':libui')

    kapt libs.arouter.compiler

    implementation 'com.github.lzyzsd:jsbridge:1.0.4'
    implementation 'com.alibaba:fastjson:1.2.78'
    implementation 'com.github.yalantis:ucrop:2.2.4'
    implementation 'com.google.android.play:app-update:2.1.0'
    implementation 'com.gyf.immersionbar:immersionbar:3.0.0'
    implementation 'com.github.li-xiaojun:XPopup:2.9.0'
    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.3'
    implementation 'com.scwang.smartrefresh:SmartRefreshHeader:1.1.3'
    implementation 'com.tencent.mm.opensdk:wechat-sdk-android:6.8.0'
    // UMeng SDK - using Maven versions where available
    implementation libs.umeng.common
    implementation libs.umeng.apm
    implementation libs.umeng.abtest

    implementation 'com.zoho.salesiq:mobilisten:7.0.0'

//    implementation 'com.topdon.umeng:abtest:1.0.1'
//    implementation 'com.topdon.aliyun:auth-number:2.13.2.1@aar'
//    implementation 'com.topdon.aliyun:logger:2.2.1:release@aar'
//    implementation 'com.topdon.aliyun:main:2.2.1:release@aar'

    //Google Firebase and services
    implementation platform('com.google.firebase:firebase-bom:28.4.1')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-messaging:21.0.1'
    implementation "com.google.firebase:firebase-iid"

    // Local AAR dependencies moved from libapp and libir
    implementation files('libs/libAC020sdk_USB_IR_1.1.1_2408291439.aar')
    implementation files('libs/libcommon_1.2.0_24052117.aar')
    implementation files('libs/libirutils_1.2.0_2409241055.aar')
    // abtest now from Maven Central - implementation files('libs/abtest-1.0.1.aar')
    implementation files('libs/auth-number-2.13.2.1.aar')
    implementation files('libs/lms_international-3.90.009.0.aar')
    implementation files('libs/logger-2.2.1-release.aar')
    implementation files('libs/main-2.2.1-release.aar')
    implementation files('libs/opengl_1.3.2_standard.aar')
    implementation files('libs/library_1.0.aar')
}