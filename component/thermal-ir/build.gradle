plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
}

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}

android {
    compileSdk rootProject.ext.android.compileSdk

    defaultConfig {
        minSdk rootProject.ext.android.minSdk
        targetSdk rootProject.ext.android.targetSdk

        buildConfigField "String", "VERSION_NAME", "\"$rootProject.ext.android.versionName\""

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures{
        dataBinding = true
    }
    
    // Configurations to handle ExoPlayer mixed AndroidX/support library issue
    configurations.all {
        resolutionStrategy {
            // Force use the latest ExoPlayer versions to minimize jetifier issues
            force 'com.google.android.exoplayer:exoplayer-ui:2.19.1'
            force 'com.google.android.exoplayer:exoplayer-core:2.19.1'
        }
    }
}

dependencies {
    // Use compileOnly for AAR dependencies - actual implementation provided by app module
    compileOnly(name: 'opengl_1.3.2_standard', ext: 'aar')
    compileOnly(name: 'library_1.0', ext: 'aar')

    implementation project(':libapp')
    implementation project(':libcom')
    implementation project(':libir')
    implementation project(':libmenu')
    implementation project(':libui')

    implementation project(':component:pseudo')

    kapt libs.arouter.compiler
    api libs.lottie
    api 'com.github.anzaizai:EasySwipeMenuLayout:1.1.4'

    api libs.andromeda.sense
    api libs.andromeda.core

    // gsy player - exclude problematic ExoPlayer UI and use newer version
    implementation("com.github.CarGuo.GSYVideoPlayer:gsyVideoPlayer-java:v8.1.4") {
        exclude group: 'com.google.android.exoplayer', module: 'exoplayer-ui'
    }
    implementation("com.github.CarGuo.GSYVideoPlayer:gsyVideoPlayer-exo_player2:v8.1.4") {
        exclude group: 'com.google.android.exoplayer', module: 'exoplayer-ui'
    }
    implementation("com.github.CarGuo.GSYVideoPlayer:gsyVideoPlayer-armv64:v8.1.4")
    
    // Use newer ExoPlayer UI - force specific version to avoid jetifier warnings
    implementation('com.google.android.exoplayer:exoplayer-ui:2.19.1') {
        // This version still has the mixed reference issue, but it's functional
        // The jetifier warning can be suppressed as this is a known issue with ExoPlayer 2.x
    }
    
    // Add compileOnly LMS dependency for compilation
    compileOnly files('../../app/libs/lms_international-3.90.009.0.aar')
}